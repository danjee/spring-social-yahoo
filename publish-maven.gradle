apply plugin: 'maven'
apply plugin: 'signing'

ext.optionalDeps = []
ext.providedDeps = []

ext.optional = { optionalDeps << it }
ext.provided = { providedDeps << it }

install {
    repositories.mavenInstaller {
        customizePom(pom, project)
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            customizePom(pom, project)
        }
    }
}

signing {
    sign configurations.archives
}

def customizePom(pom, gradleProject) {
    pom.whenConfigured { generatedPom ->
        // respect 'optional' and 'provided' dependencies
        gradleProject.optionalDeps.each { dep ->
            generatedPom.dependencies.findAll { it.artifactId == dep.name }*.optional = true
        }
        gradleProject.providedDeps.each { dep ->
            generatedPom.dependencies.findAll { it.artifactId == dep.name }*.scope = 'provided'
        }

        // eliminate test-scoped dependencies (no need in maven central poms)
        generatedPom.dependencies.removeAll { dep ->
            dep.scope == 'test'
        }

        // add all items necessary for maven central publication
        generatedPom.project {
            name = 'Spring Social Yahoo'
            packaging = 'jar'
            description = 'Spring Social Yahoo is an extension based on Spring Social that provides functionality to ' +
                    'communicate with the Yahoo Social Rest API'
            url = 'https://github.com/gabrielruiu/spring-social-yahoo.git'
            organization {
                name = 'Gabriel Ruiu'
                url = 'https://github.com/gabrielruiu'
            }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                url = 'https://github.com/gabrielruiu/spring-social-yahoo.git'
                connection = 'scm:git:git://github.com/gabrielruiu/spring-social-yahoo.git'
                developerConnection = 'scm:git:git://github.com/gabrielruiu/spring-social-yahoo.git'
            }
            developers {
                developer {
                    id = 'gabrielruiu'
                    name = 'Gabriel Ruiu'
                    email = 'gabriel.ruiu@mail.com'
                }
            }
        }
    }
}
